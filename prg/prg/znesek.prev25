# Copyright (c) 2025 Prev25 Wizards
fun putint(x: int): void
fun puts(str: ^char): void
fun putchar(c: char): void

fun init_coins(): void =
    coins[0] = 200,
    coins[1] = 100,
    coins[2] = 50,
    coins[3] = 20,
    coins[4] = 10,
    coins[5] = 5,
    coins[6] = 2,
    coins[7] = 1

fun izpis_kombinacije(sol: ^[8]int): void =
    let var i: int in
        i = 0,
        while i < 8 do
            if sol^[i] != 0 then
                putint(coins[i]),
                puts("c * "),
                putint(sol^[i]),
                if (i != 7) then
                    puts(" || ")
                end
            end,
            i = i + 1
        end
    end,
    putchar('\0x0A')

fun solve(amnt: int, coin: int, sol: ^[8]int): int =
    if coin == 7 then
        sol^[coin] = amnt,
        # izpiÅ¡emo kombinacijo
        izpis_kombinacije(sol),
        return 1
    end,

    let
        var num_sol: int
        var c: int
    in
        num_sol = 0,
        c = 0,
        while c <= (amnt / coins[coin]) do
            sol^[coin] = c,
            num_sol = num_sol + solve(amnt - (c * coins[coin]), coin + 1, sol),
            c = c + 1
        end,
        return num_sol
    end

var coins: [8]int
var comb: [8]int
fun main(): int =
    init_coins(),
    putint(solve(10, 0, ^comb)),
    putchar('\0x0A'),
    return 0